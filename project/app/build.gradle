
plugins {
    id("com.github.triplet.play") version "2.6.2"
    id("com.android.application")
    id("io.objectbox")
    id("kotlin-android")
    id("kotlin-android-extensions")
    id("kotlin-kapt")
    id("com.hiya.jacoco-android")
}

def versionMajor = 2
def versionMinor = 2
def versionPatch = 0
def versionBuild = 0 // This value is managed by the gradle publisher plugin. Build numbers get incremented on publish

android {
    compileSdkVersion 29
    ndkVersion "21.3.6528147" //TODO wait for google to fix bug in AGP 4.0.0: https://issuetracker.google.com/issues/144111441
    defaultConfig {
        applicationId "org.owntracks.android"
        minSdkVersion 21
        targetSdkVersion 29

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'org.owntracks.android.EventBusIndex']
            }
        }
        resConfigs "en", "de", "fr", "es"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
/* TODO Get this lot sorted when the orchestrator / coverage / clearPackageData bug gets fixed */
//        testInstrumentationRunnerArguments clearPackageData: 'true'
//        testInstrumentationRunnerArguments coverageFilePath: '/sdcard/'
//        testInstrumentationRunnerArguments coverage: 'true'
    }

    signingConfigs {
        debug {
            keyAlias 'debug.owntracks.org'
            keyPassword '__DEBUG__'
            storeFile file('./debug-keystore.jks')
            storePassword '__DEBUG__'
        }
        release {
            keyAlias 'upload'
            keyPassword System.getenv("KEYSTORE_PASSPHRASE")
            storeFile file('../owntracks.release.keystore.jks')
            storePassword System.getenv("KEYSTORE_PASSPHRASE")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "GOOGLE_MAPS_API_KEY", "${google_maps_api_key != null ? google_maps_api_key : "PLACEHOLDER_API_KEY"}"
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "GOOGLE_MAPS_API_KEY", "${google_maps_api_key != null ? google_maps_api_key : "PLACEHOLDER_API_KEY"}"
            applicationIdSuffix '.debug'
            testCoverageEnabled true
            signingConfig signingConfigs.debug
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    lintOptions {
        baseline file("../../lint/lint-baseline.xml")
        checkAllWarnings true
        warningsAsErrors false
        abortOnError false
        disable 'TypographyFractions', 'TypographyQuotes', 'Typos'

    }
    testOptions {
        animationsDisabled = true
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true

        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed"
                exceptionFormat "full"
            }
            reports {
                junitXml.enabled = true
                html.enabled = false
            }
        }
//        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildToolsVersion = '29.0.3'
}

kapt {
    correctErrorTypes = true
}

apply from: "publishing.gradle"
apply from: "dependencies.gradle"
apply from: "connectedTestScreenshotTasks.gradle"