apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'
apply plugin: 'org.greenrobot.greendao'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 1
def versionBuild = 2
ext.daggerVersion = '2.6'
ext.playLibVersion = '9.4.0'
ext.okHttpVersion = '3.4.1'
ext.supportLibVersion = '24.1.1'


android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "org.owntracks.android"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            minifyEnabled false
        }

    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        main {
            jniLibs.srcDir 'jniLibs'
            java.srcDirs = ['src/main/java', 'build/generated/source/greendao/org/owntracks/android/db']
        }
    }

    signingConfigs {
        debug {
            keyAlias 'debug.owntracks.org'
            keyPassword '__DEBUG__'
            storeFile file('./debug-keystore.jks')
            storePassword '__DEBUG__'
        }
    }
    packagingOptions {
        // Remove unneeded Realm native libs (nearly no devices use these)
        exclude 'lib/armeabi/librealm-jni.so'
        exclude 'lib/mips/librealm-jni.so'
        exclude 'lib/x86/librealm-jni.so'
        exclude 'lib/x86_64/librealm-jni.so'
    }
}
repositories {
    maven { url 'https://repo.eclipse.org/content/repositories/paho-snapshots/' }
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {

    compile files('jniLibs/kalium-jni-1.0.0-SNAPSHOT.jar', )

    // Support libraries
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"

    // Play Services libraries
    compile "com.google.android.gms:play-services-gcm:${playLibVersion}"
    compile "com.google.android.gms:play-services-maps:${playLibVersion}"
    compile "com.google.android.gms:play-services-location:${playLibVersion}"
    compile "com.google.android.gms:play-services-places:${playLibVersion}"
    compile "com.google.android.gms:play-services-base:${playLibVersion}"


    // Utility libraries
    compile 'org.greenrobot:eventbus:3.0.0'
    apt 'org.greenrobot:eventbus-annotation-processor:3.0.1'

    compile 'com.google.zxing:android-integration:3.2.1' // Code scanning
    compile 'org.greenrobot:greendao:3.0.1'
    compile 'org.altbeacon:android-beacon-library:2.7.1@aar'
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.1-20160821.041725-20'

    compile 'com.squareup.okhttp3:okhttp:3.4.1'

    compile 'com.fasterxml.jackson.core:jackson-core:2.7.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
    compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter:1.1.0'
    compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter-recyclerview:1.1.0'
    compile 'com.jakewharton.timber:timber:4.1.2'


    // Widget libraries
    compile 'com.github.rengwuxian:MaterialEditText:2.1.4'
    compile('com.mikepenz:fastadapter:1.1.2@aar') {
        transitive = true
    }
    compile 'com.mikepenz:materialdrawer:5.0.5@aar'
    compile 'com.mikepenz:materialize:0.8.8@aar'
    compile 'com.mikepenz:iconics-core:2.5.11@aar'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.8@aar') {
        transitive = true
    }
    compile('com.github.afollestad.material-dialogs:commons:0.8.5.8@aar') {
        transitive = true
    }
    compile 'com.joanzapata.android:android-iconify:1.0.10'
    compile 'com.obsez.android.lib.filechooser:filechooser:1.1.2'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    compile "ch.acra:acra:4.8.5"

    apt 'com.google.guava:guava:19.0' // needed by Dagger
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    compile "com.google.dagger:dagger:${daggerVersion}"

    compile 'io.reactivex:rxjava:1.1.8'
    compile 'io.reactivex:rxandroid:1.2.1'

}
apt {
    arguments {
        eventBusIndex "org.owntracks.android.EventBusIndex"
    }
}


greendao {
    schemaVersion 15
}